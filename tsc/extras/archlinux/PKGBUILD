# Maintainer: Quintus <quintus@quintilianus.eu>
pkgname=tsc
pkgver=2.0.0
pkgrel=1
pkgdesc="Jump'n'run game with editor and scripting facilities, fork of SMC"
arch=("i686" "x86_64")
url="http://www.secretchronicles.de"
license=('GPL')
depends=("sfml" "devil" "boost-libs" "libxml++" "glew" "pcre" "gtk-update-icon-cache")
makedepends=("cmake" "ruby" "gperf" "pkg-config" "bison" "boost" "gettext")
optdepends=()
conflicts=()
options=()
source=("ftp://ftp.secretchronicles.de/releases/TSC-$pkgver.tar.xz" "tsc.desktop" "fix-manpage.patch")
noextract=()
install="tsc.install"
sha256sums=("c8a9ca3975f9de16aa59fab2a7864c3c541963b46c2c40533632c4fc621b407a"
	    "2484bbe4f94c561f27df1e155073f4166c927cd79626ed05aa42f0328e38d55f"
	    "eb4ac81523e4367ebc9c907e1a4a1d08fec3ddcbca83febaef81eb727a2a8c1f")

prepare() {
    cd "$srcdir"

    # This patch will be included in TSC itself in the next version.
    # It can then be removed from this PKGBUILD.
    patch -p1 < ../fix-manpage.patch
}

build() {
    cd "$srcdir/TSC-$pkgver/tsc"
    mkdir build
    cd build
    cmake -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Release ..
    make
}

package() {
    cd "$srcdir/TSC-$pkgver/tsc/build"
    make install DESTDIR="$pkgdir/"
}

# Copyright Â© 2013-2015 The TSC Contributors
#
# This program is free software; you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation; either version 3 of the License, or
# (at your option) any later version.
#
# You should have received a copy of the GNU General Public License
# along with this program.  If not, see <http://www.gnu.org/licenses/>.
