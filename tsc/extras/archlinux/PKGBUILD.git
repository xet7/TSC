# Maintainer: Quintus <quintus@quintilianus.eu>
pkgname=tsc-git
pkgver=r1032
pkgrel=1
pkgdesc="Jump'n'run game with editor and scripting facilities, fork of SMC"
arch=("i686" "x86_64")
url="http://www.secretchronicles.de"
license=('GPL')
depends=("sfml" "devil" "boost-libs" "libxml++" "glew" "pcre" "gtk-update-icon-cache")
makedepends=("cmake" "ruby" "gperf" "pkg-config" "bison" "boost" "gettext")
optdepends=()
conflicts=()
options=()
source=("git+https://github.com/Secretchronicles/TSC.git" "tsc.desktop")
noextract=()
install="tsc.install"
sha256sums=("SKIP"
	    "2484bbe4f94c561f27df1e155073f4166c927cd79626ed05aa42f0328e38d55f")

pkgver() {
    cd "$srcdir/TSC"
    echo -n "r"
    git rev-list HEAD --count
}

prepare() {
    cd "$srcdir/TSC"
    git submodule init
    git submodule update
}

build() {
    cd "$srcdir/TSC/tsc"
    mkdir build
    cd build
    cmake -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Debug ..
    make
}

package() {
    cd "$srcdir/TSC/tsc/build"
    make install DESTDIR="$pkgdir/"
}

# Copyright Â© 2013-2015 The TSC Contributors
#
# This program is free software; you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation; either version 3 of the License, or
# (at your option) any later version.
#
# You should have received a copy of the GNU General Public License
# along with this program.  If not, see <http://www.gnu.org/licenses/>.
